WHITESPACE = _{ " " | "\t" | NEWLINE | "," }
COMMENT = _{";" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }
edn = { (SOI ~ expr ~ EOI) | SOI ~ EOI }
expr = _{ string | character | number | list | vector | map | set | symbol | keyword | tagged_element}

// TODO: parse nil and bools
literal = { nil | boolean } 
nil = { "nil" }
boolean = { "true" | "false" }

string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

character = @{ "\\" ~ ("newline"|"return"|"space"|"tab"|("u"~ASCII_HEX_DIGIT{4})|ANY)}

// [:]?([\D&&[^/]].*/)?([\D&&[^/]][^/]*)
keyword = @{ ":" ~ ksymbol }
ksymbol = @{ ((kprefix ~ "/") ~ ":"? ~ kname) | kname }
kprefix = @{ ksym }
kname = @{ ksym | "/" }
ksym = @{ ("+"|"-"|".")? ~ ksym_first_char ~ sym_char* } 
ksym_first_char = @{ ASCII_ALPHA|"."|"*"|"+"|"!"|"-"|"_"|"?"|"$"|"%"|"&"|"="|"<"|">"|"#"}

symbol = ${ (prefix ~ "/")? ~ name }
prefix = ${ sym }
name = ${ sym | "/" }
sym = @{ (("+"|"-"|".")? ~ sym_first_char ~ sym_char*) | (("+"|"-"|".") ~ !ASCII_DIGIT) } 
sym_char = @{ ASCII_ALPHANUMERIC|"."|"*"|"+"|"!"|"-"|"_"|"?"|"$"|"%"|"&"|"="|"<"|">"|"#"|":"}
sym_first_char = @{ ASCII_ALPHA|"."|"*"|"+"|"!"|"-"|"_"|"?"|"$"|"%"|"&"|"="|"<"|">"}


number = ${ float | ratio | integer }
//  ([-+]?)(?:(0)|([1-9][0-9]*)|0[xX]([0-9A-Fa-f]+)|0([0-7]+)|([1-9][0-9]?)[rR]([0-9A-Za-z]+)|0[0-9]+)(N)?
integer = @{ ("-" | "+")? ~ (decimal | hex | octal | radix ) ~ "N"?}
decimal = @{ "0" | (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)}
hex = @{ "0" ~ ^"x" ~ ASCII_HEX_DIGIT+ }
octal = @{ "0" ~ ('0'..'7')+ }
radix = @{ (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT?) ~ ^"r" ~ ASCII_HEX_DIGIT+ }

// ([-+]?[0-9]+)/([0-9]+)
ratio = @{ ("-" | "+")? ~ ASCII_DIGIT+ ~ "/" ~ ASCII_DIGIT+ }

// ([-+]?[0-9]+(\.[0-9]*)?([eE][-+]?[0-9]+)?)(M)?
float = @{ ("-" | "+")? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ ("-" | "+")? ~ ASCII_DIGIT+ )? ~ "M"?}

list = { "(" ~ expr* ~ ")" }
vector = { "[" ~ expr* ~ "]" }
map = { "{" ~ (expr ~ expr)* ~ "}" }
set = { "#{" ~ expr* ~ "}" }

tagged_element = { tag ~ expr }
tag = @{ "#" ~ !"/" ~ symbol }